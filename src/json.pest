WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

json = _{ object | array }
object = { "{" ~ (pair ~ ("," ~ pair)*) ~ "}" }
pair = { string ~ ":" ~ value }
array = { "[" ~ (value ~ ("," ~ value)*) ~ "]" }

value = { string | number | object | array | bool | null }

// string is a sequence of characters wrapped in double quotes
string = _{ "\"" ~ chars ~ "\"" }
chars = @{ (unescaped | escaped)* }
// any char not start with backslash or double quote
unescaped = { !("\\" | "\"") ~ ANY }
escaped = { "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t" | "u" ~ ASCII_HEX_DIGIT{4}) }

// number cloud have a sign, integer part, fraction part, and exponent part
number = @{ "-"? ~ integer ~ fraction? ~ exponent? }
integer = { "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
fraction = { "." ~ ASCII_DIGIT+ }
exponent = { ("e" | "E") ~ ("+" | "-")? ~ ASCII_DIGIT+ }

bool = { "true" | "false" }
null = { "null" }
